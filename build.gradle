buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.franzbecker:gradle-lombok:1.10"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.10.RELEASE")
    }
}

group 'com.wilqor.workshop.bytebay'
version '1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: "io.franzbecker.gradle-lombok"
apply plugin: 'org.springframework.boot'


sourceCompatibility = 1.8


repositories {
    mavenCentral()
}


dependencies {
    //commons compress, for parsing bzipped files
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.16.1'

    // Lucene dependencies
    compile 'org.apache.lucene:lucene-core:7.2.1'
    compile 'org.apache.lucene:lucene-analyzers-common:7.2.1'
    compile 'org.apache.lucene:lucene-facet:7.2.1'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version: '7.2.1'


    // Lombok
    compileOnly 'org.projectlombok:lombok:1.16.20'

    // Json
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.0'

    // Logging
    compile 'org.apache.logging.log4j:log4j-api:2.7'
    compile 'org.apache.logging.log4j:log4j-core:2.7'

    // Yaml
    compile 'org.yaml:snakeyaml:1.19'

    //spring boot
    compile("org.springframework.boot:spring-boot-starter-web")

    // test dependencies
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

def wikisearchapp = "$projectDir/src/main/wikisearch"

sourceSets {
    main {
        resources {
            srcDirs = ["$wikisearchapp/dist", "$projectDir/src/main/resources"]
        }
    }
}


task buildAngular(type:Exec) {
    // installAngular should be run prior to this task
    dependsOn "installAngular"
    workingDir "$wikisearchapp"
    inputs.dir "$wikisearchapp"
    // Add task to the standard build group
    group = BasePlugin.BUILD_GROUP
    // ng doesn't exist as a file in windows -> ng.cmd
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine "ng.cmd", "build"
    } else {
        commandLine "ng", "build"
    }
}

task startAngular(type:Exec) {
    // installAngular should be run prior to this task
    dependsOn "buildAngular"
    workingDir "$wikisearchapp"
    inputs.dir "$wikisearchapp"
    // Add task to the standard build group

    // ng doesn't exist as a file in windows -> ng.cmd
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine "ng.cmd", "serve"
    } else {
        commandLine "ng", "serve"
    }
}

task installAngular(type:Exec) {
    workingDir "$wikisearchapp"
    inputs.dir "$wikisearchapp"
    group = BasePlugin.BUILD_GROUP
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine "npm.cmd", "install"
    } else {
        commandLine "npm", "install"
    }
}
